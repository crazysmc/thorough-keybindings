plugins {
  id 'fabric-loom' version libs.versions.loom
  id 'ploceus' version libs.versions.loom
}

version = "$mod_version+mc${fabricLibs.versions.minecraft.get()}"
group = maven_group
base.archivesName = mod_id

def ap = project ':ap'

dependencies {
  minecraft fabricLibs.minecraft
  mappings loom.officialMojangMappings()

  modImplementation libs.loader

  implementation ap
  annotationProcessor ap
  testImplementation libs.junit
}

java {
  toolchain.languageVersion = JavaLanguageVersion.of 8
  withSourcesJar()
}

test {
  useJUnitPlatform()
}

processResources {
  inputs.property 'properties', project.properties.toString()
  filesMatching 'fabric.mod.json', {
    expand project.properties + [range: fabricLibs.versions.range.get()]
  }
}

runClient {
  systemProperty 'mixin.debug.export', true
  systemProperty 'mixin.debug.export.filter', 'net.minecraft.**'
}

tasks.withType Jar configureEach {
  from rootProject.file('LICENSE')
  from ap.tasks.compileJava.outputs
}
