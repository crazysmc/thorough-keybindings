plugins {
  id 'fabric-loom' version libs.versions.loom
  id 'ploceus' version libs.versions.loom
}

version = "$mod_version+${libs.versions.minecraft.get()}"
group = maven_group
base.archivesName = mod_id

def ap = project ':ap'

if (libs.versions.hasProperty 'split') {
  loom.clientOnlyMinecraftJar()
  ploceus.clientOnlyMappings()
}

dependencies {
  minecraft libs.minecraft
  mappings ploceus.featherMappings(libs.versions.feather.get())
  if (libs.versions.nests.get())
    nests ploceus.nests(libs.versions.nests.get())

  modImplementation libs.loader
  ploceus.addCommonLibraries()
  ploceus.dependOsl libs.versions.osl.get()

  implementation ap
  annotationProcessor ap
  testImplementation libs.junit
}

java {
  toolchain.languageVersion = JavaLanguageVersion.of 8
  withSourcesJar()
}

def generatedJava = layout.buildDirectory.dir 'generated/sources/versioned/java'
def generatedResources = layout.buildDirectory.dir 'generated/sources/versioned/resources'

sourceSets {
  versioned {
    java { compileClasspath += main.compileClasspath + main.runtimeClasspath }
  }
  main.java.srcDir generatedJava
  main.resources.srcDir generatedResources
}
idea.module.generatedSourceDirs += generatedJava.get().asFile

def mc = ploceus.normalizedMinecraftVersion()
println "Minecraft: $mc"

def preprocessJava = tasks.register 'preprocessJava', PreprocessorTask, {
  group = BasePlugin.BUILD_GROUP
  minecraft = mc
  from sourceSets.versioned.java
  into generatedJava
}

def preprocessResources = tasks.register 'preprocessResources', PreprocessorTask, {
  group = BasePlugin.BUILD_GROUP
  minecraft = mc
  from sourceSets.versioned.resources
  into generatedResources
}

def preprocess = tasks.register 'preprocess', {
  group = BasePlugin.BUILD_GROUP
  dependsOn preprocessJava, preprocessResources
}

tasks.named 'compileJava', { dependsOn preprocess }
tasks.named 'sourcesJar', { dependsOn preprocess }
tasks.named 'processResources', { dependsOn preprocessResources }

test {
  useJUnitPlatform()
}

processResources {
  inputs.property 'properties', project.properties.toString()
  filesMatching 'fabric.mod.json', {
    expand project.properties + [range: libs.versions.range.get()]
  }
}

runClient {
  systemProperty 'mixin.debug.export', true
  systemProperty 'mixin.debug.export.filter', 'net.minecraft.**'
  args += ['--userProperties', '{}']
}

tasks.withType Jar configureEach {
  from rootProject.file('LICENSE')
  from ap.tasks.compileJava.outputs
}
