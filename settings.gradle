pluginManagement {
  repositories {
    maven {
      name = 'Fabric'
      url = 'https://maven.fabricmc.net/'
    }
    maven {
      name = 'Ornithe Releases'
      url = 'https://maven.ornithemc.net/releases'
    }
    maven {
      name = 'Ornithe Snapshots'
      url = 'https://maven.ornithemc.net/snapshots'
    }
    gradlePluginPortal()
  }
}

dependencyResolutionManagement {
  versionCatalogs {
    libs {
      version 'loom', '1.6-SNAPSHOT'
      library 'loader', 'net.fabricmc', 'fabric-loader' version '0.15.9'
      library 'junit', 'org.junit.jupiter', 'junit-jupiter' version '5.10.2'

      def fabric = { v, f -> [mc: v, r: "~$v", fapi: f] }

      def i = startParameter.projectProperties.preset as Integer ?: 0
      def conf = [
          fabric('1.14.4', '0.28.5+1.14'),
      ][i]
      // for p in {0..0}; do ./gradlew :mod:build -Ppreset=$p; done

      version 'minecraft', conf.mc
      version 'range', conf.r
      library 'minecraft', 'com.mojang', 'minecraft' versionRef 'minecraft'
      library 'fapi', 'net.fabricmc.fabric-api', 'fabric-api' version conf.fapi
    }
  }
}

include ':ap'
include ':mod'
