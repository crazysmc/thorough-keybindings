pluginManagement {
  repositories {
    maven {
      name = 'Fabric'
      url = 'https://maven.fabricmc.net/'
    }
    maven {
      name = 'Ornithe Releases'
      url = 'https://maven.ornithemc.net/releases'
    }
    maven {
      name = 'Ornithe Snapshots'
      url = 'https://maven.ornithemc.net/snapshots'
    }
    gradlePluginPortal()
  }
}

dependencyResolutionManagement {
  versionCatalogs {
    libs {
      version 'loom', '1.6-SNAPSHOT'
      library 'loader', 'net.fabricmc', 'fabric-loader' version '0.15.9'
      library 'junit', 'org.junit.jupiter', 'junit-jupiter' version '5.10.2'

      def i = startParameter.projectProperties.preset as Integer ?: 0
      def presets = [
          [mc: '1.0.0', nests: '7', split: '1', label: '1.0-1.2', range: '>=1.0 <1.3'],
          [mc: '1.3.1', nests: '1', label: '1.3'],
          [mc: '1.4.2', nests: '1', label: '1.4-1.5', range: '>=1.4.2 <1.6'],
          [mc: '1.6.1', nests: '1', label: '1.6'],
          [mc: '1.7.2', nests: '1', range: '=1.7.2'],
          [mc: '1.7.4', nests: '1', label: '1.7.4-1.7.9', range: '>=1.7.4 <1.7.10'],
          [mc: '1.7.10', nests: '1'],
          [mc: '1.8', nests: '1', label: '1.8-1.8.1', range: '>=1.8 <1.8.2'],
          [mc: '1.8.2', label: '1.8.2-1.8.9'],
          [mc: '1.9', label: '1.9-1.10', range: '>=1.9 <1.11'],
          [mc: '1.11'],
          [mc: '1.12'],
          [mc: '1.13'],
      ]
      def conf = presets[i]
      // for p in {0..11}; do ./gradlew :mod:build -Ppreset=$p; done

      version 'minecraft', (conf.label ?: conf.mc)
      version 'range', (conf.range ?: "~$conf.mc")
      library 'minecraft', 'com.mojang', 'minecraft' version conf.mc
      version 'feather', (conf.feather ?: conf.split ? '21' : '26')
      version 'nests', (conf.nests ?: '')
      if (conf.split) version 'split', 'yes'

      version 'osl', '0.14.2'
    }
  }
}

include ':ap'
include ':mod'
